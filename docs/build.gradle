plugins {
    id 'java'
    id 'io.spring.antora.generate-antora-yml' version '0.0.1'
    id 'org.antora' version '1.0.0'
    id 'java-library'
}

description = "Micrometer Docs"

antora {
    version = '3.2.0-alpha.2'
    playbook = 'cached-antora-playbook.yml'
    playbookProvider {
        repository = 'micrometer/micrometer-docs'
        branch = 'docs-build'
        path = 'lib/antora/templates/per-branch-antora-playbook.yml'
        checkLocalBranch = true
    }
    options = ['--clean', '--stacktrace']
    /*
    environment = [
            //'ALGOLIA_API_KEY': '82c7ead946afbac3cf98c32446154691',
            //'ALGOLIA_APP_ID': '244V8V9FGG',
            //'ALGOLIA_INDEX_NAME': 'micrometer-docs'
    ]
    */
    dependencies = [
            '@antora/atlas-extension': '1.0.0-alpha.1',
            '@antora/collector-extension': '1.0.0-alpha.3',
            '@asciidoctor/tabs': '1.0.0-beta.3',
            '@opendevise/antora-release-line-extension': '1.0.0-alpha.2',
            '@springio/antora-extensions': '1.3.0',
            '@springio/asciidoctor-extensions': '1.0.0-alpha.9'
    ]
}


tasks.named("generateAntoraYml") {
    asciidocAttributes = project.provider( {
        return  ["micrometer-version": project.version]
    } )
}

tasks.create("generateAntoraResources") {
    dependsOn 'generateAntoraYml'
}

jar {
    enabled = false
}

javadoc {
    enabled = false
}

repositories {
   mavenCentral()
}

dependencies {
    implementation 'io.micrometer:micrometer-core'
    implementation 'io.micrometer:context-propagation'
    implementation 'io.micrometer:micrometer-tracing'
    implementation 'ch.qos.logback:logback-classic'
    implementation 'org.slf4j:slf4j-api'

    testImplementation 'io.zipkin.reporter2:zipkin-sender-urlconnection'

    testImplementation 'io.micrometer:micrometer-test'
    testImplementation 'io.micrometer:micrometer-observation-test'
    testImplementation 'io.micrometer:micrometer-tracing-test'
    testImplementation 'io.micrometer:micrometer-tracing-integration-test'
    testImplementation 'org.aspectj:aspectjweaver'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-inline'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.springframework:spring-context'
    testImplementation 'io.projectreactor:reactor-core'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'io.netty:netty-transport'
    testImplementation 'org.apache.httpcomponents.client5:httpclient5'
    testImplementation 'io.javalin:javalin'
}

