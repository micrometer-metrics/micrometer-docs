def VERSIONS = [
        'io.micrometer:context-propagation:1.0.+',
        'com.github.tomakehurst:wiremock-jre8-standalone:2.33.+',
        'org.apache.httpcomponents.client5:httpclient5:5.1.+',
        'io.javalin:javalin:4.+',
        'io.projectreactor:reactor-core:latest.release',
        'io.projectreactor:reactor-test:latest.release',
        'ch.qos.logback:logback-classic:1.2.+',
        'org.asciidoctor:asciidoctorj:latest.release',
        'org.springframework:spring-context:5.+',
        'org.aspectj:aspectjweaver:1.8.+',
        'org.junit.jupiter:junit-jupiter:5.8.+', // Not using latest.release to avoid using a milestone version.
        'org.junit.platform:junit-platform-launcher:1.8.+',
        'org.assertj:assertj-core:latest.release',
        'org.mockito:mockito-core:latest.release',
        'org.mockito:mockito-inline:latest.release',
        'org.slf4j:slf4j-api:1.7.+',
]

subprojects {
  plugins.withId('java-library') {
    dependencies {
      constraints {
        // Direct dependencies
        VERSIONS.each {version->
          // java-library plugin has three root configurations, so we apply constraints too all of
          // them so they all can use the managed versions.
          api version
          compileOnly version
          runtimeOnly version
        }
      }
      implementation platform('io.micrometer:micrometer-bom:1.10.1-SNAPSHOT')
      implementation platform('io.micrometer:micrometer-tracing-bom:1.0.1-SNAPSHOT')
    }
  }
}
