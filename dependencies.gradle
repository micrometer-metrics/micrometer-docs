def VERSIONS = [
        'ch.qos.logback:logback-classic:1.2.+',
        'org.asciidoctor:asciidoctorj:latest.release',
        'org.junit.jupiter:junit-jupiter:5.8.+', // Not using latest.release to avoid using a milestone version.
        'org.junit.platform:junit-platform-launcher:1.8.+',
        'org.assertj:assertj-core:latest.release',
        'org.mockito:mockito-core:latest.release',
        'org.mockito:mockito-inline:latest.release',
        'org.slf4j:slf4j-api:1.7.+',
]

subprojects {
  plugins.withId('java-library') {
    dependencies {
      constraints {
        // Direct dependencies
        VERSIONS.each {version->
          // java-library plugin has three root configurations, so we apply constraints too all of
          // them so they all can use the managed versions.
          api version
          compileOnly version
          runtimeOnly version
        }
      }
      implementation platform('io.micrometer:micrometer-bom:1.10.0-SNAPSHOT')
      implementation platform('io.micrometer:micrometer-tracing-bom:1.0.0-SNAPSHOT')
      implementation platform('io.micrometer:micrometer-docs-generator-bom:1.0.0-SNAPSHOT')
    }
  }
}
